{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Variable Selection: \n",
    "### Backwards Elimination SAS Macro\n",
    "\n",
    "#### Purpose:\n",
    "The macro performs an automated backward elimination variable selection process for `PROC GENMOD` which does not come with model selection options. Note that the `GENMOD` procedure in SAS versions prior to 9.4 does not come with model selection options. \n",
    "### Introduction:\n",
    "SAS users of SAS 9.2 and prior versions may face situations where some \"powerful\" options are only available in certain SAS procedures but not available in others. For example, the model selection options are available in `PROC REG`, `LOGISTIC`, `PHREG`, etc., but not in `PROC GENMOD`, `CATMOD`, `MIXED`, etc. This backwards selection macro could be used with the procedures `GENNMOD`, `CATMOD`, `MIXED`, `GLIMMIX`, etc.\n",
    "### Illustration:\n",
    "The following SAS statements simulate 5000 observations, which are based on an underlying Tweedie generalized linear model (GLM) that exploits its connection with the compound Poisson distribution. A natural logarithm link function is assumed for modeling the response variable (`yTweedie`), and there are five categorical variables (`C1–C5`), each of which has four numerical levels and two continuous variables (`D1` and `D2`). By design, two of the categorical variables, `C3` and `C4`, and one of the two continuous variables, `D2`, have no effect on the response. The dispersion parameter is set to 0.5, and the power parameter is set to 1.5."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "%let nObs = 5000;\n",
    "%let nClass = 5;\n",
    "%let nLevs = 4;\n",
    "%let seed = 1234;\n",
    "\n",
    "data tmp1;\n",
    "   array c{&nClass};\n",
    "\n",
    "   keep c1-c&nClass yTweedie d1 d2;\n",
    "\n",
    "   /* Tweedie parms */\n",
    "   phi=0.5;\n",
    "   p=1.5;\n",
    "\n",
    "   do i=1 to &nObs;\n",
    "\n",
    "      do j=1 to &nClass;\n",
    "         c{j} = int(ranuni(1)*&nLevs);\n",
    "      end;\n",
    "\n",
    "      d1 = ranuni(&seed);\n",
    "      d2 = ranuni(&seed);\n",
    "\n",
    "      xBeta = 0.5*((c2<2) - 2*(c1=1) + 0.5*c&nClass + 0.05*d1);\n",
    "      mu = exp(xBeta);\n",
    "\n",
    "      /* Poisson distributions parms */\n",
    "      lambda = mu**(2-p)/(phi*(2-p));\n",
    "      /* Gamma distribution parms */\n",
    "      alpha = (2-p)/(p-1);\n",
    "      gamma = phi*(p-1)*(mu**(p-1));\n",
    "\n",
    "      rpoi = ranpoi(&seed,lambda);\n",
    "      if rpoi=0 then yTweedie=0;\n",
    "      else do;\n",
    "         yTweedie=0;\n",
    "         do j=1 to rpoi;\n",
    "         yTweedie = yTweedie + rangam(&seed,alpha);\n",
    "         end;\n",
    "         yTweedie = yTweedie * gamma;\n",
    "      end;\n",
    "      output;\n",
    "   end;\n",
    "run;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The following code generates a basic explanatory data analysis for the dependent and independent variables:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "/* EDA */\n",
    "\n",
    "%let var_char = yTweedie c1 c2 c3 c4 c5 d1 d2;\n",
    "\n",
    "%put &var_char;\n",
    "\n",
    "\n",
    "data var_char;\n",
    "\tset tmp1\n",
    "\t(keep= &var_char);     \n",
    "run;\n",
    "\n",
    "proc contents data = var_char varnum nodetails noprint \n",
    "out=var_char_names (keep=name);\n",
    "run;\n",
    "\n",
    "data var_char_names;\n",
    "\tset var_char_names;\n",
    "\tj = _n_;\n",
    "run;\n",
    "\n",
    "* Determine the number of observations;\n",
    "data _NULL_;\n",
    "\tif 0 then set var_char_names nobs=n;\n",
    "\tcall symputx('nrows',n);\n",
    "\tstop;\n",
    "run;\n",
    "\n",
    "%put &nrows;\n",
    "\n",
    "%macro do_eda_uni;\n",
    "%do obs = 1 %to &nrows;\n",
    "\n",
    "data _null_;\n",
    "\tset var_char_names;\n",
    "\tif j = &obs  then call symputx(\"var\", put(name, 10.));\n",
    "run;\n",
    "%if (%upcase(&var)=YTWEEDIE) or (%upcase(&var)=D1) or (%upcase(&var)=D2)   %then %do; \n",
    "\n",
    "\tods graphics on;\n",
    "\t\tproc means data=tmp1 fw=12 printalltypes chartype\n",
    "\t   \t\tqmethod=os maxdec=2\n",
    "\t\n",
    "\t\t\tmean \n",
    "\t\t\tmin \n",
    "\t\t\tmax \n",
    "\t\t\tmode \n",
    "\t\t\trange \n",
    "\t\t\tn \n",
    "\t\t\tnmiss\t\n",
    "\t\t\tp1 \n",
    "\t\t\tp5 \n",
    "\t\t\tmedian \n",
    "\t\t\tp95 \n",
    "\t\t\tp99\t;\n",
    "\t\t\tvar &var;\n",
    "\t\trun;\n",
    "\n",
    "\t\ttitle \"histograms\";\n",
    "\t\tproc univariate data=tmp1\tnoprint;\n",
    "\t\t\tvar &var;\n",
    "\t\t\thistogram ;\n",
    "\t\trun; \n",
    "\tods graphics off;\n",
    "\t%end;\n",
    "\n",
    "\t%else %do;\n",
    "\tods graphics on;\n",
    "\t\tproc freq data=tmp1\n",
    "\t\torder=internal;\n",
    "\t\ttables &var /  scores=table plots(only)=freq;\n",
    "\t\trun;\n",
    "\tods graphics off;\n",
    "\t%end;\n",
    "\n",
    "%end;\n",
    "%mend do_eda_uni;\n",
    "\n",
    "%do_eda_uni;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The histogram for the `yTweedie` dependent variable:\n",
    "\n",
    "<img src=\"images/be01.png\" alt=\"Mountain View\" style=\"width:450px\">\n",
    "\n",
    "The independent character variable `c1-c5`:\n",
    "\n",
    "<img src=\"images/be02.png\" alt=\"Mountain View\" style=\"width:450px\">\n",
    "<img src=\"images/be03.png\" alt=\"Mountain View\" style=\"width:450px\">\n",
    "<img src=\"images/be04.png\" alt=\"Mountain View\" style=\"width:450px\">\n",
    "<img src=\"images/be05.png\" alt=\"Mountain View\" style=\"width:450px\">\n",
    "<img src=\"images/be06.png\" alt=\"Mountain View\" style=\"width:450px\">\n",
    "\n",
    "The histogram for the two independent continuous variables `d1` and `d2`:\n",
    "\n",
    "<img src=\"images/be07.png\" alt=\"Mountain View\" style=\"width:450px\">\n",
    "<img src=\"images/be08.png\" alt=\"Mountain View\" style=\"width:450px\">\n",
    "\n",
    "The next lines contain the two SAS macros for the backwards elimination selection process using a Tweedie error function. \n",
    "\n",
    "The first macro `%MdStmt` is a stand-alone macro. The main macro, `%MdSelect`, consists of multiple calls to the macro `%MdStmt`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "/* Variable Selection Macro: Backwards elimination */\n",
    "%let p=1.5;\n",
    "options mlogic;\n",
    "%macro MdStmt(\n",
    " \t    resvar = /*response variable */\n",
    "\t   ,expvar = /*list of explanatory variables, separated by ' ' */\n",
    " \t   ,clsvar = /*classification variables in the CLASS statement separated by ' ' */\n",
    "\t   ,p = \n",
    "\t   );\n",
    "\n",
    "        ods output Type3=pval(rename=source=parm);\n",
    "\t\tproc genmod data=tmp1 NAMELEN=50; \n",
    "\t\t\tif _resp_ > 0 then \n",
    "\t\t\td = 2*(_resp_*(_resp_**(1-&p)-_mean_**(1-&p))/\n",
    "\t\t\t(1-&p)-(_resp_**(2-&p)-_mean_**(2-&p))/(2-&p)); \n",
    "\t\t\telse d = 2* _mean_**(2-&p)/(2-&p);\n",
    "\t\t\tvariance var = _mean_**&p;\n",
    "\t\t\tdeviance dev = d;\n",
    "\t\t\tclass &clsvar; \t\n",
    "\t\t\tmodel &resvar =  &expvar /link=log type3 scale=pearson;                 \n",
    "\t\t\t*scwgt expos;\n",
    "            title \"&resvar = &expvar\";\t\n",
    "\t\trun;\n",
    " \t\tods output close;\n",
    " %mend MdStmt;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "There are five macro parameters in the macro %MdSelect: `&VAR`, `&INTVAR`, `&CATVAR`, `&SLSTAY` and `&POWER`:\n",
    "\n",
    "- `&VAR` is the response variable which will be passed into `&RESVAR` when calling the macro `%MdStmt`;\n",
    "- `&INTVAR` includes all the potential explanatory variables which will be passed into `&EXPVAR` in `%MdStmt` only forthe first call;\n",
    "- `&CATVAR` contains all the categorical explanatory variables which will be passed into `%CLSVAR` in `%MdStmt`;\n",
    "- `&SLSTAY` is the criteria for removing variable;\n",
    "- and `&POWER` is the power parameter of the Tweedie distribution"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "%macro MdSelect(\n",
    " \t   var= /*response variable */\n",
    "\t   ,intvar= /*initial explanatory variables for full model */\n",
    " \t   ,catvar= /*categorical explanatory variables */\n",
    " \t   ,slstay= /*criterion for removing variable */\n",
    "\t   ,power=\n",
    " \t   );\n",
    "\t%let var=%upcase(&var);\n",
    " \t%let intvar=%upcase(&intvar);\n",
    " \t%let catvar=%upcase(&catvar);\n",
    "\t%let power =&power; \n",
    "%*-------------------------------------------------------------------------*;\n",
    "%* Create empty dataset \"step\" with only one column \"parm\". It will be *;\n",
    "%* merged with \"pval\" from PROC GENMOD by \"parm\" *;\n",
    "%*-------------------------------------------------------------------------*;\n",
    " proc sql;\n",
    " \tcreate table step_&var (parm char(9));\n",
    " quit;\n",
    "%*------------------------------------------------------------------------------*;\n",
    "%* %do %until performs multivariate backward model selection: *;\n",
    "%* In each iteration: *;\n",
    "%* 1. Run the logistic regression model *;\n",
    "%* 2. Update the dataset \"step_&var\" *;\n",
    "%* 3. Create &pmax as the maximum p-value, and &varlist as the list of *;\n",
    "%* variables without the one with the max p-value *;\n",
    "%* 4. Check whether the max p-value <= &SLSTAY *;\n",
    "%* 5. If NO, then eliminate the variable with max p-value, repeat step 1 to 4.*;\n",
    "%* If YES, the loop stops *;\n",
    "%*------------------------------------------------------------------------------*;\n",
    " %let i=1;\n",
    " %do %until (&pmax<=&slstay);\n",
    "\n",
    "\t%if &i = 1 %then\n",
    " \t\t%MdStmt(resvar=&var ,expvar=&intvar, clsvar=&catvar, p=&power); %*initial model;\n",
    "\t%else %do;\n",
    " \t\t%MdStmt(resvar=&var ,expvar=&varlist, clsvar=&catvar, p=&power); %*reduced model;\n",
    "\t%end;\n",
    " \tproc sort data=step_&var; by parm;\n",
    " \tproc sort data=pval; by parm;\n",
    " \tdata step_&var;\n",
    " \t\tmerge step_&var pval;\n",
    " \t\tby parm;\n",
    " \t\tp&i=put(ProbChiSq, pvalue6.3);\n",
    " \t\tdrop ProbChiSq ChiSq DF;\n",
    " \trun;\n",
    " \tproc sql noprint;\n",
    " \t\tselect max(ProbChiSq) into :pmax\n",
    " \t\tfrom pval;  \n",
    " \t\tselect distinct parm into :varlist separated by ' '\n",
    " \t\tfrom pval\n",
    " \t\thaving ProbChiSq^=max(ProbChiSq);\n",
    " \tquit;\n",
    "\n",
    "\t%let i=%eval(&i+1);\n",
    "\n",
    " %end;\n",
    " proc print data=step_&var;\n",
    " \ttitle \"&var: model selection process\";\n",
    " run;\n",
    "%mend MdSelect; \n",
    "\n",
    "%MdSelect(var=yTweedie, intvar=c1 c2 c3 c4 c5 d1 d2, catvar=c1 c2 c3 c4 c5, slstay=0.05, power=1.5); "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The execution of the above two macros create two outputs:\n",
    "- A summary table of the model selection process\n",
    "- The whole model selection process step by step\n",
    "The summary table of the model selection process:\n",
    "\n",
    "<img src=\"images/be09.png\" alt=\"Mountain View\" style=\"width:450px\">\n",
    "\n",
    "The table shows that the variable C4 is eliminated in the second step of the process. The variable D2 is eliminated in the third step. And the variable C3 is eliminated in the fourth step. After the fourth step the algorithm arrive at final main effects model.\n",
    "\n",
    "The whole variable selection process step by step:\n",
    "\n",
    "<img src=\"images/be10.png\" alt=\"Mountain View\" style=\"width:310px\">\n",
    "<img src=\"images/be11.png\" alt=\"Mountain View\" style=\"width:310px\">\n",
    "<img src=\"images/be12.png\" alt=\"Mountain View\" style=\"width:650px\">\n",
    "<img src=\"images/be13.png\" alt=\"Mountain View\" style=\"width:600px\">\n",
    "<img src=\"images/be14.png\" alt=\"Mountain View\" style=\"width:400px\">\n",
    "<img src=\"images/be15.png\" alt=\"Mountain View\" style=\"width:280px\">\n",
    "<img src=\"images/be16.png\" alt=\"Mountain View\" style=\"width:350px\">\n",
    "<img src=\"images/be17.png\" alt=\"Mountain View\" style=\"width:600px\">\n",
    "<img src=\"images/be18.png\" alt=\"Mountain View\" style=\"width:600px\">\n",
    "<img src=\"images/be19.png\" alt=\"Mountain View\" style=\"width:400px\">\n",
    "<img src=\"images/be20.png\" alt=\"Mountain View\" style=\"width:250px\">\n",
    "<img src=\"images/be21.png\" alt=\"Mountain View\" style=\"width:350px\">\n",
    "<img src=\"images/be22.png\" alt=\"Mountain View\" style=\"width:600px\">\n",
    "<img src=\"images/be23.png\" alt=\"Mountain View\" style=\"width:600px\">\n",
    "<img src=\"images/be24.png\" alt=\"Mountain View\" style=\"width:400px\">\n",
    "<img src=\"images/be25.png\" alt=\"Mountain View\" style=\"width:220px\">\n",
    "<img src=\"images/be26.png\" alt=\"Mountain View\" style=\"width:310px\">\n",
    "<img src=\"images/be27.png\" alt=\"Mountain View\" style=\"width:600px\">\n",
    "<img src=\"images/be28.png\" alt=\"Mountain View\" style=\"width:600px\">\n",
    "<img src=\"images/be29.png\" alt=\"Mountain View\" style=\"width:420px\">\n",
    "\n",
    "Conclusion:\n",
    "The above lines shows how the variable selection algorithm eliminates those variables (`C3`, `C4` and `D2`) no associated with the dependent variable `yTweedie` - remember that the illustrative dataset was arterially created with this aim. Therefore, the macro works accurately. \n",
    "\n",
    "The SAS macros `%MdStmt` and `%MdSelect`:\n",
    "\n",
    "- Performs a backwards elimination variable selection process\n",
    "- The last step in the elimination process shows the selected model and a summary table of the elimination process\n",
    "- The macro needs around 15 minutes to get results with a dataset of one million observations and around 13 variables\n",
    "- The elimination criteria is based on the p-values of the type 3 analysis\n",
    "- With small changes the macro is useful in a context with a `GENMOD` procedure under Gamma, Inverse Gaussian, Log-Normal, Binomial, Gaussian, Poisson, Negative Binomial, Zero Inflated Poisson and Zero inflated Negative Binomial error functions.\n",
    "- This macro could be useful as a template to create Forward and Stepwise variable selection processes\n",
    "- One drawback of the backwards elimination process is that if the full model with all potential main factors does not converge the macro does not work. That is one of the reasons because a forward option is interesting\n",
    "- The specification of the model is the same that the Tweedie macro used in the `NAR` project\n",
    "- This macro only admits main factors. So, it is not possible to include interactions in the model statement of the GENMODE procedure. To include interactions it is needed create a new variable with the interaction\n",
    "\n",
    "### References:\n",
    "\n",
    "A detailed explanation of the algorithm and the code appears here:\n",
    "\n",
    "[Using Macro and ODS to Overcome Limitations of SAS® Procedures Jing Su and Wei (Lisa) Lin, Merck & Co, Inc., North Wales, PA](http://www.lexjansen.com/nesug/nesug07/cc/cc26.pdf) \n",
    "\n",
    "The dataset for the example comes from here:\n",
    "\n",
    "http://support.sas.com/documentation/cdl/en/statug/68162/HTML/default/viewer.htm#statug_genmod_examples12.htm\n",
    "\n",
    "I made some changes in order to get coherence results. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python [py3k]",
   "language": "python",
   "name": "Python [py3k]"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.5.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
